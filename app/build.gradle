plugins {
    id "com.android.application"
}
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        applicationId "com.sketchware.remod"
        namespace "com.sketchware.remod"
        //noinspection ExpiredTargetSdkVersion since we don't target getting Sketchware Pro on Google Play.
        targetSdkVersion 28
        versionCode 150
        versionName "v6.4.0-SNAPSHOT-" + getCommitHash()

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        buildConfigField("String", "FLAVOR_NAME_WITH_AABS", "\"minApi26\"")
        buildConfigField("String", "FLAVOR_NAME_WITHOUT_AABS", "\"minApi21\"")

        buildConfigField("String", "VERSION_NAME_WITHOUT_FLAVOR", "\"v6.4.0-SNAPSHOT-${getCommitHash()}\"")
    }

    flavorDimensions "api"
    productFlavors {
        minApi26 {
            dimension "api"
            minSdkVersion 26
            versionNameSuffix "-minApi26"
        }

        minApi21 {
            dimension "api"
            minSdkVersion 21
            versionNameSuffix "-minApi21"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        resources.excludes += "license/*"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    androidResources {
        additionalParameters "--stable-ids", "public-stable-ids.txt"
    }

    signingConfigs {
        debug {
            storeFile file("../testkey.keystore")
            storePassword "testkey"
            keyAlias "testkey"
            keyPassword "testkey"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "com.android.tools:annotations:30.2.1"
    implementation "com.google.guava:guava:31.1-jre"

    def codeEditorVersion = "0.15.1"
    def excludeAnnotation = { exclude group: "androidx.annotation", module: "annotation" }
    def editorGroupId = "io.github.Rosemoe.sora-editor"

    implementation "$editorGroupId:editor:$codeEditorVersion", excludeAnnotation
    implementation "$editorGroupId:language-java:$codeEditorVersion", excludeAnnotation
    implementation "$editorGroupId:language-textmate:$codeEditorVersion"

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.github.angads25:filepicker:1.1.1"

    minApi26Implementation "com.android.tools.build:bundletool:1.9.1"
    //noinspection GradleDependency
    implementation "com.android.tools:sdklib:25.3.0", { exclude group: "com.intellij", module: "annotations" }
    implementation "com.android.tools:r8:3.3.28"

    implementation "com.google.code.gson:gson:2.9.0"

    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    //noinspection GradleDependency
    implementation "com.squareup.okio:okio:3.1.0"

    implementation "com.guardsquare:proguard-base:7.1.1"

    //noinspection GradleDependency
    minApi21Implementation "org.eclipse.jdt:ecj:3.13.0"
    minApi26Implementation "org.eclipse.jdt:ecj:3.26.0"

    minApi26Implementation project(path: ":kotlinc")

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
}
