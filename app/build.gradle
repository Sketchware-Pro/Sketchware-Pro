def secrets = System.getenv("GOOGLE_SERVICES_JSON")
def analyticsEnabled = secrets != null

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

if (analyticsEnabled) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

android {
    compileSdk 33

    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        applicationId "com.sketchware.remod"
        namespace "com.sketchware.remod"
        //noinspection ExpiredTargetSdkVersion since we don't target getting Sketchware Pro on Google Play.
        targetSdkVersion 28
        versionCode 150
        versionName "v6.4.0-SNAPSHOT-" + getCommitHash()

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        buildConfigField("String", "FLAVOR_NAME_WITH_AABS", "\"minApi26\"")
        buildConfigField("String", "FLAVOR_NAME_WITHOUT_AABS", "\"minApi21\"")

        buildConfigField("String", "VERSION_NAME_WITHOUT_FLAVOR", "\"v6.4.0-SNAPSHOT-${getCommitHash()}\"")

        buildConfigField("String", "CRASH_REPORT_WEBHOOK_URL", "\"${System.getenv("CRASH_REPORT_WEBHOOK_URL")}\"")
    }

    flavorDimensions "api"
    productFlavors {
        minApi26 {
            dimension "api"
            minSdkVersion 26
            versionNameSuffix "-minApi26"
            isDefault = true
        }

        minApi21 {
            dimension "api"
            minSdkVersion 21
            versionNameSuffix "-minApi21"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        resources.excludes += "license/*"
        resources.excludes += "META-INF/DEPENDENCIES"
        resources.pickFirsts += "api_database/*"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    androidResources {
        additionalParameters "--stable-ids", "public-stable-ids.txt"
    }

    signingConfigs {
        debug {
            storeFile file("../testkey.keystore")
            storePassword "testkey"
            keyAlias "testkey"
            keyPassword "testkey"
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"], exclude: "proguard-base-7.2.2.jar")

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.9.0"
    implementation "com.airbnb.android:lottie:3.1.0"
    implementation "com.google.android.flexbox:flexbox:3.0.0"

    implementation "androidx.annotation:annotation:1.6.0"
    implementation "com.android.tools:annotations:31.0.2"
    implementation "com.google.guava:guava:32.0.1-jre"

    def editorGroupId = "io.github.Rosemoe.sora-editor"
    implementation platform("$editorGroupId:bom:0.21.1-f243159-SNAPSHOT")
    implementation "$editorGroupId:editor"
    implementation "$editorGroupId:language-java"
    implementation "$editorGroupId:language-textmate"

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.github.angads25:filepicker:1.1.1"

    minApi26Implementation "com.android.tools.build:bundletool:1.15.1"
    //noinspection GradleDependency copied from above dependency for not just runtime access
    minApi26Implementation "com.google.protobuf:protobuf-java:3.19.2"
    //noinspection GradleDependency
    implementation "com.android.tools:sdklib:25.3.0", { exclude group: "com.intellij", module: "annotations" }
    implementation "com.android.tools:r8:8.0.40"
    implementation "com.github.Iyxan23:zipalign-java:1.0.0"

    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.madgag:scpkix-jdk15on:1.47.0.2"

    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    //noinspection GradleDependency
    implementation "com.squareup.okio:okio:3.1.0"
    implementation "com.github.Cosmic-Ide:DependencyResolver:868996895a"

    minApi21Implementation "net.sf.proguard:proguard-base:6.0.3"
    minApi26Implementation files('libs/proguard-base-7.2.2.jar')
    minApi26Implementation 'com.guardsquare:proguard-core:9.0.2'
    minApi26Implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22'

    //noinspection GradleDependency
    minApi21Implementation "org.eclipse.jdt:ecj:3.13.0"
    minApi26Implementation "org.eclipse.jdt:ecj:3.26.0"

    minApi26Implementation project(path: ":kotlinc")

    implementation "com.github.topjohnwu.libsu:core:5.0.4"

    // Analytics and Crashlytics
    if (analyticsEnabled) {
        new File("$projectDir/google-services.json").text = secrets
    }
    implementation platform("com.google.firebase:firebase-bom:31.2.1")
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs_nio:2.0.3"
}
